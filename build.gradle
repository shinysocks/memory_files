plugins {
    id 'java'
    id 'eclipse'
    id 'application'
    id 'java-library'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

ext {
    projectName = 'memory_files'
}


eclipse {
    project {
        name = "$projectName"
    }

    classpath {
        defaultOutputDir = file("${project.projectDir}/build/eclipse")
        file {
            whenMerged {
                cp -> cp.getEntries().forEach{
                    cpEntry -> if(cpEntry.kind=='src') {
                        cpEntry.output = cpEntry.output.replace('bin/', 'build/eclipse/')
                    }
                }
            }
        }
    }
}

application {
    mainClass = "Main"
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

version = '1.0.0'
java.sourceCompatibility = "22"

javafx {
    version = "22.0.2"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.base', 'javafx.swing', 'javafx.graphics', 'javafx.media' ]
}

run {
    standardInput = System.in
}

/** 
 * :uberjar - packages JavaFX programs [needs Main.java entrypoint]
 */
task uberjar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'Main'
    }
    archiveBaseName = "$projectName"
    archiveClassifier = ""
    archiveVersion = ""
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }

        resources {
            srcDirs = ['src']
        }
    }
}

